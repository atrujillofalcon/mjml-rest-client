import net.researchgate.release.GitAdapter

buildscript {
    ext.kotlin_version = '1.2.41'
    ext.spring_version = '5.0.7.RELEASE'
    ext.jackson_version = '2.9.6'
    ext.thymeleaf_version = '3.0.9.RELEASE'

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'net.researchgate.release' version '2.6.0'
    id "com.jfrog.bintray" version "1.7.3"
}

group 'es.atrujillo.mjml'
version project.property('version')


apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    //Spring dependencies
    compile "org.springframework:spring-web:$spring_version"

    //Jackson dependencies
    compile "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    compile "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"

    compile "org.apache.httpcomponents:httpclient:4.5.5"
    compile "javax.validation:validation-api:2.0.1.Final"

    //Template with thymeleaf
    compile "org.thymeleaf:thymeleaf:$thymeleaf_version"

    compile "org.slf4j:slf4j-api:1.7.25"

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
    testCompile 'org.mockito:mockito-core:2.18.3'
    testCompile "org.springframework:spring-test:5.0.6.RELEASE"

}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

//Script to generate valid Maven Central pom.xml
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "atrujillo"
            name "Arnaldo Trujillo"
            email "atrujillo92work@gmail.com"
        }
    }

    scm {
        url "https://github.com/atrujillofalcon/mjml-rest-client"
    }
}


publishing {
    publications {
        MavenCentral(MavenPublication) {
            from components.java
            groupId 'es.atrujillo.mjml'
            artifactId 'mjml-rest-client'
            version project.property('version')
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Java library that use the mjml API')
                root.appendNode('name', 'Mjml Rest Client')
                root.appendNode('url', 'https://github.com/atrujillofalcon/mjml-rest-client')
                root.children().last() + pomConfig
            }
        }
    }
}
//End of script to generate valid Maven Central pom.xml

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    publications = ['MavenCentral']
    pkg {
        repo = 'mjml-rest-client'
        name = 'mjml-rest-client-release'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/atrujillofalcon/mjml-rest-client.git'
    }
}

release {
    failOnCommitNeeded = true
    failOnPublishNeeded = true
    failOnSnapshotDependencies = true
    failOnUnversionedFiles = false
    failOnUpdateNeeded = true
    revertOnFail = true
    preCommitText = ''
    preTagCommitMessage = '[Gradle Release Plugin] - pre tag commit: '
    tagCommitMessage = '[Gradle Release Plugin] - creating tag: '
    newVersionCommitMessage = '[Gradle Release Plugin] - new version commit: '
    tagTemplate = '${version}'
    versionPropertyFile = 'gradle.properties'
    scmAdapters = [
            GitAdapter,
    ]

    git {
        requireBranch = 'develop'
        pushToRemote = 'origin'
        pushToBranchPrefix = ''
        commitVersionFileOnly = false
    }
}

afterReleaseBuild.dependsOn bintrayUpload

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}